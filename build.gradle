import java.util.concurrent.TimeUnit

def configDir = new File(rootDir, 'config')

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }

    dependencies {
        classpath 'me.trnl:clirr-gradle-plugin:0.4'
        classpath 'com.antwerkz.github:github-release-gradle-plugin:1.0.0-RC3'
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.3'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    configurations {
        provided
        provided.extendsFrom(compile)
    }
   
    configurations.all {
        // check for updates every build
        resolutionStrategy.cacheChangingModulesFor 0, TimeUnit.MILLISECONDS
        resolutionStrategy.cacheDynamicVersionsFor 0, TimeUnit.MILLISECONDS
    }

    task clearDependencies << {
        configurations.compile.each { File file -> 
            println "removing ${file}"
            file.delete() }
    }
    
    task listDependencies << {
        configurations.compile.each { File file -> println file }
    }
}

def slf4jVersion = '1.7.5'

configure(subprojects) {
    apply plugin: 'checkstyle'
    apply plugin: 'findbugs'

    group = 'org.mongodb.morphia'
    version = '0.106-SNAPSHOT'

    sourceCompatibility = '1.5'
    targetCompatibility = '1.5'

    configurations {
        provided
        provided.extendsFrom(compile)
    }

    project.archivesBaseName = "morphia-${project.name}"

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }

    dependencies {
        testCompile 'junit:junit:4.11'
    }

    sourceSets.main.compileClasspath += configurations.provided
    sourceSets.test.compileClasspath += configurations.provided
    sourceSets.test.runtimeClasspath += configurations.provided

    if (properties['xlint']) {
        tasks.withType(Compile) {
            options.compilerArgs << "-Xlint:unchecked"
        }
    }

    project.ext.buildingWith = { n -> project.hasProperty(n) && project.property(n).toBoolean()
    }

    /* Code quality */
    checkstyle {
        configFile = new File("$configDir/checkstyle.xml")
    }

    findbugs {
        excludeFilter = new File("$configDir/findbugs-exclude.xml")
    }

    tasks.withType(Test) { 
        if (System.getenv()['RS_ENABLED'] == "true") {
            systemProperty 'MONGO_URI', 'mongodb://localhost:27017,localhost:27018,localhost:27019'
        }
    }

    tasks.withType(FindBugs) {
        reports {
            xml.enabled = project.buildingWith('xmlReports.enabled')
            html.enabled = !project.buildingWith('xmlReports.enabled')
        }
    }

    javadoc {
        options.author = true
        options.version = true
        options.links 'http://docs.oracle.com/javase/7/docs/api/'
    }

    test {
        // set heap size for the test JVM(s)
        minHeapSize = "256m"
        maxHeapSize = "512m"

        // set JVM arguments for the test JVM(s)
        jvmArgs '-XX:MaxPermSize=256m'
    }

}

project(':morphia') {
    archivesBaseName = 'morphia'

    dependencies {
        if (project.hasProperty('3.x')) {
            compile group: "org.mongodb", name: "mongo-java-driver", version: "3.0.0-SNAPSHOT", changing: true
            println "Building with the 3.x java driver"
        } else if (project.hasProperty('2.x')) {
            compile group: "org.mongodb", name: "mongo-java-driver", version: "2.12.0-SNAPSHOT", changing: true
            println "Building with the 2.x java driver"
        } else {
            compile 'org.mongodb:mongo-java-driver:[2.10.1,2.12)'
        }

        provided 'cglib:cglib-nodep:[2.1_3,3.0)'
        provided 'com.thoughtworks.proxytoys:proxytoys:1.0'
        provided 'com.google.code.findbugs:findbugs:2.0.2'
    }
}

project(':logging-slf4j') {
    dependencies {
        compile project(':morphia')

        compile "org.slf4j:slf4j-api:$slf4jVersion"
        testCompile "org.slf4j:slf4j-simple:$slf4jVersion"
    }
}

project(':validation') {
    dependencies {
        compile 'javax.validation:validation-api:1.0.0.GA'
        testCompile 'org.hibernate:hibernate-validator:4.1.0.Final'
        compile project(':morphia')
        compile project(':logging-slf4j')
        testCompile "org.slf4j:slf4j-jdk14:${slf4jVersion}"
        
        if(System.getProperty('java.version').startsWith("1.5")) {
            println "Including extra dependencies for the older JVM"

            testCompile 'com.sun.xml.bind:jaxb-impl:2.2.4-1'
            testCompile 'javax.xml.bind:jaxb-api:2.2.4'
            testCompile 'javax.xml.soap:saaj-api:1.3.3'
        }
    }
}

project(':entityscanner-plug') {
    dependencies {
        compile('org.reflections:reflections:0.9.8') {
            'servlet-api:javax.servlet'
            'com.google.code.gson:gson'
            'logback-classic:ch.qos.logback'
            'xml-apis:xml-apis'
        }
        compile 'com.google.collections:google-collections:1.0'
        compile('org.scannotation:scannotation:1.0.3') {
            'javassist:javassist'
        }
        compile project(':morphia')
        testCompile "org.slf4j:slf4j-simple:${slf4jVersion}"
    }
}

project(':guice-plug') {
    dependencies {
        compile 'com.google.inject:guice:3.0'
        compile project(':morphia')
    }
}

project(':jrebel-plug') {}

project(':no-proxy-deps-tests') {
   
    sourceSets {
        main {
            output.classesDir '../morphia/build/classes/test'
        }
    }
    dependencies {
        compile project(':morphia')
    }
}

project(':gwt') {
    sourceSets {
        main {
            java { srcDirs = [] }
        }
    }
}

apply from: 'gradle/ide-settings.gradle'
apply from: 'gradle/maven-deployment.gradle'
apply from: 'gradle/osgi-compatibility.gradle'
apply from: 'gradle/release-process.gradle'
