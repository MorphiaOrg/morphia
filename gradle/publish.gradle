/*
 * Copyright (c) 2008 - 2013 10gen, Inc. <http://10gen.com>
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

configure(subprojects) {
    apply plugin: 'nexus'
    apply plugin: 'signing'

    project.ext.mavenDeployers = []

    def pom = { config ->
        project.mavenDeployers*.pom config
    }

    install {
        project.mavenDeployers << repositories.mavenInstaller
    }

    uploadArchives {
        project.mavenDeployers << repositories.mavenDeployer {
            def nexusCredentials = [
                    userName: project.properties.nexusUsername,
                    password: project.properties.nexusPassword
            ]

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
                authentication(nexusCredentials)
            }
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(nexusCredentials)
            }
        }
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            }
        }
    }

    modifyPom {
        project {
            name = 'Morphia'
            description = 'Java Object Document Mapper for MongoDB'
            url 'https://github.com/mongodb/morphia.git'

            scm {
                url 'https://github.com/mongodb/morphia.git'
                connection 'scm:git:https://github.com/mongodb/morphia.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    name 'Various'
                    organization = 'MongoDB'
                }
            }
        }

        whenConfigured { resultPom ->
            resultPom.dependencies.removeAll { dep -> dep.scope != 'compile' }
            resultPom.dependencies*.scope = null
        }
    }
}
